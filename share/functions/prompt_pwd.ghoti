function prompt_pwd --description "Print the current working directory, shortened to fit the prompt"
    set -l options h/help
    argparse -n prompt_pwd --max-args=0 $options -- $argv
    or return

    if set -q _flag_help
        __ghoti_print_help prompt_pwd
        return 0
    end

    # This allows overriding ghoti_prompt_pwd_dir_length from the outside (global or universal) without leaking it
    set -q ghoti_prompt_pwd_dir_length
    or set -l ghoti_prompt_pwd_dir_length 1

    # Replace $HOME with "~"
    set -l realhome ~
    set -l tmp (string replace -r '^'"$realhome"'($|/)' '~$1' $PWD)

    if [ $ghoti_prompt_pwd_dir_length -eq 0 ]
        echo $tmp
    else
        # Shorten to at most $ghoti_prompt_pwd_dir_length characters per directory
        string replace -ar '(\.?[^/]{'"$ghoti_prompt_pwd_dir_length"'})[^/]*/' '$1/' $tmp
    end
end
