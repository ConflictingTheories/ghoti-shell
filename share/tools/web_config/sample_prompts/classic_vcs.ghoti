# name: Classic + Vcs (the default)
# author: Lily Ballard

function ghoti_prompt --description 'Write out the prompt'
    set -l last_pipestatus $pipestatus
    set -lx __ghoti_last_status $status # Export for __ghoti_print_pipestatus.
    set -l normal (set_color normal)

    # Color the prompt differently when we're root
    set -l color_cwd $ghoti_color_cwd
    set -l suffix '>'
    if functions -q ghoti_is_root_user; and ghoti_is_root_user
        if set -q ghoti_color_cwd_root
            set color_cwd $ghoti_color_cwd_root
        end
        set suffix '#'
    end

    # If we're running via SSH, change the host color.
    set -l color_host $ghoti_color_host
    if set -q SSH_TTY
        set color_host $ghoti_color_host_remote
    end

    # Write pipestatus
    # If the status was carried over (e.g. after `set`), don't bold it.
    set -l bold_flag --bold
    set -q __ghoti_prompt_status_generation; or set -g __ghoti_prompt_status_generation $status_generation
    if test $__ghoti_prompt_status_generation = $status_generation
        set bold_flag
    end
    set __ghoti_prompt_status_generation $status_generation
    set -l prompt_status (__ghoti_print_pipestatus "[" "]" "|" (set_color $ghoti_color_status) (set_color $bold_flag $ghoti_color_status) $last_pipestatus)

    echo -n -s (set_color $ghoti_color_user) "$USER" $normal @ (set_color $color_host) (prompt_hostname) $normal ' ' (set_color $color_cwd) (prompt_pwd) $normal (ghoti_vcs_prompt) $normal " "$prompt_status $suffix " "
end
